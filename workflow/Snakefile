REFERENCE_FASTA_PATH = dict(
    CHM13="/home/miaocj/docker_dir/data/reference_genome/CHM13/chm13v2.0.fa.gz",
)
REGIONS = dict(
    TRA=("chr14", 14819498, 17749884),
    HLA=("chr6", 28476949, 34231258),
    IGK=("chr2", 87866370, 91790947),
    IGH=("chr14", 98839469, 101161492),
    chr6_50M=("chr6", 0, 50_000_000),
    chr1_248M=("chr1", 0, 1_000_000_000)
)


wildcard_constraints:
    length_kb=r"\d+",
    sample=r"\w+",
    region=r"\w+",
    platform=r"\w+",
    encoding=r"\w+",


rule extract_regional_reference:
    input:
        fasta=lambda wildcards: REFERENCE_FASTA_PATH[wildcards["sample"]],
    output:
        fasta="data/regional_reference/{sample}/{region}/reference.fasta.gz",
    params:
        region=lambda wildcards: REGIONS[wildcards["region"]],
    conda:
        "envs/default.yaml"
    script:
        "scripts/extract_regional_reference.smk.py"


rule simulate_perfect_sequencing_reads:
    input:
        fasta="data/regional_reference/{sample}/{region}/reference.fasta.gz",
    output:
        fasta="data/regional_reads/{sample}/{region}/perfect_{length_kb}k/reads.fasta.gz",
    params:
        length_kb=lambda wildcards: int(wildcards["length_kb"]),
        depth=30,
        seed=3558929,
    wildcard_constraints:
        length_kb=r"\d+",
    conda:
        "envs/default.yaml"
    script:
        "scripts/simulate_perfect_sequencing_reads.smk.py"


rule run_pbsim3:
    input:
        reference="data/regional_reference/{sample}/{region}/reference.fasta.gz",
        model="resources/pbsim/ERRHMM-ONT-HQ.model",
    output:
        folder=directory(
            "data/regional_reads/{sample}/{region}/pbsim_ONT_{accuracy}_{length_kb}k"
        ),
        fasta="data/regional_reads/{sample}/{region}/pbsim_ONT_{accuracy}_{length_kb}k/reads.fasta.gz",
        maf="data/regional_reads/{sample}/{region}/pbsim_ONT_{accuracy}_{length_kb}k/alignment.maf.gz",
    wildcard_constraints:
        accuracy=r"\d+",
        length_kb=r"\d+",
    params:
        accuracy=lambda wildcards: float(wildcards["accuracy"]) / 100,
        length_mean=lambda wildcards: int(wildcards["length_kb"]) * 1000,
        length_sd=lambda wildcards: int(wildcards["length_kb"]) * 500,
        length_min=10_000,
        depth=20,
        seed=5023967,
    conda:
        "envs/pbsim.yaml"
    shell:
        """
        gunzip -c {input.reference} \
        | pbsim \
            --strategy wgs \
            --method errhmm \
            --errhmm {input.model} \
            --depth {params.depth} \
            --accuracy-mean {params.accuracy} \
            --length-mean {params.length_mean} \
            --length-sd  {params.length_sd} \
            --length-min {params.length_min} \
            --genome /dev/stdin \
            --seed {params.seed} \
            --prefix {output.folder}/output
        sed -n '1~4s/^@/>/p;2~4p' {output.folder}/output_0001.fastq | gzip -c > {output.fasta}
        gzip -c {output.folder}/output_0001.maf > {output.maf}
        rm -rf {output.folder}/output*
        """


rule get_perfect_read_info:
    input:
        fasta="data/regional_reads/{sample}/{region}/{platform}/reads.fasta.gz",
    output:
        tsv="data/regional_reads/{sample}/{region}/{platform}/read_info.tsv.gz",
    wildcard_constraints:
        platform=r"perfect_.+",
    conda:
        "envs/default.yaml"
    script:
        "scripts/get_perfect_read_info.smk.py"


rule get_pbsim_metadata:
    input:
        #fasta="data/regional_reads/{sample}/{region}/{platform}/reads.fasta.gz",
        maf="data/regional_reads/{sample}/{region}/{platform}/alignment.maf.gz",
    output:
        tsv="data/regional_reads/{sample}/{region}/{platform}/read_info.tsv.gz",
    wildcard_constraints:
        platform=r"pbsim_.+",
    conda:
        "envs/default.yaml"
    script:
        "scripts/get_pbsim_read_info.smk.py"


rule sampling_fastq:
    input:
        ont="/home/miaocj/docker_dir/HG002.ONT_R9.fastq.gz"
    output:
        fastq="data/regional_reads/{sample}/{region}/{platform}/reads.fastq.gz"
    shell:
        """
        seqtk sample -s 100 {input.ont} 0.43 | gzip > {output.fastq} &
        """


rule alignment_of_real_data_and_reference:
    input:
         reference="data/regional_reads/{sample}/{region}/{platform}/genome.fasta",
         fastq="data/regional_reads/{sample}/{region}/{platform}/reads.fastq.gz"
    output:
         paf="data/regional_reads/{sample}/{region}/{platform}/alignment.paf.gz"
    conda:
        "envs/minimap2.yaml"
    threads: 8
    shell:
        """
        minimap2 -x map-ont -t {threads} {input.reference} {input.fastq} | gzip -c > {output.paf}
        """

rule fastq_to_fasta:
    input:
        fastq="data/regional_reads/{sample}/{region}/{platform}/reads.fastq.gz",
    output:
        fasta="data/regional_reads/{sample}/{region}/{platform}/reads.fasta.gz",
    shell:
        """
        seqtk -a {input.fastq} | gzip > {output.fasta}
        """

rule paf_to_metadata:
    input:
        fasta="data/regional_reads/{sample}/{region}/{platform}/reads.fasta.gz",
        paf="data/regional_reads/{sample}/{region}/{platform}/alignment.paf.gz",
    output:
        tsv="data/regional_reads/{sample}/{region}/{platform}/read_info.tsv.gz",
        stat="data/regional_reads/{sample}/{region}/{platform}/read_usage_statistic.tsv.gz"
    conda:
        "envs/default.yaml"
    script:
        "scripts/get_real_read_info.smk.py"

rule kmer_encoding:
    input:
        fasta="data/regional_reads/{sample}/{region}/{platform}/reads.fasta.gz",
        tsv="data/regional_reads/{sample}/{region}/{platform}/read_info.tsv.gz",
        paf="data/regional_reads/{sample}/{region}/{platform}/alignment.paf.gz",
    output:
        npz="data/feature_matrix/{sample}/{region}/{platform}/kmer_k{k}/feature_matrix.npz",
        json="data/feature_matrix/{sample}/{region}/{platform}/kmer_k{k}/read_features.json.gz",
        tsv="data/feature_matrix/{sample}/{region}/{platform}/kmer_k{k}/metadata.tsv.gz",
    params:
        sample_fraction=0.05,
        min_multiplicity=2,
        seed=562104830,
    wildcard_constraints:
        k=r"\d+",
    conda:
        "envs/default.yaml"
    script:
        "scripts/kmer_encoding.smk.py"

rule kmer_encoding_test:
    input:
        fasta="data/regional_reads/{sample}/{region}/{platform}/reads.fasta.gz",
        tsv="data/regional_reads/{sample}/{region}/{platform}/read_info1.tsv.gz",
        paf="data/regional_reads/{sample}/{region}/{platform}/alignment.paf.gz",
    output:
        npz="data/feature_matrix/{sample}/{region}/{platform}/kmer_k{k}/feature_matrix.npz",
        json="data/feature_matrix/{sample}/{region}/{platform}/kmer_k{k}/read_features.json.gz",
        tsv="data/feature_matrix/{sample}/{region}/{platform}/kmer_k{k}/metadata1.tsv.gz",
    params:
        sample_fraction=0.05,
        min_multiplicity=2,
        seed=562104830,
    wildcard_constraints:
        k=r"\d+",
    conda:
        "envs/default.yaml"
    script:
        "scripts/kmer_encoding_for_chrs.smk.py"


rule run_minimap2_ava:
    input:
        fasta="data/regional_reads/{sample}/{region}/{platform}/reads.fasta.gz",
    output:
        paf_minimap2="data/minimap2/{sample}/{region}/{platform}/alignment.paf.gz",
    params:
        preset="ava-ont",
    conda:
        "envs/minimap2.yaml"
    threads: 8
    shell:
        """
        minimap2 -x {params.preset} -t {threads} {input.fasta} {input.fasta} | gzip -c > {output.paf_minimap2}
        """

rule blend:
    input:
        fasta="data/regional_reads/{sample}/{region}/{platform}/reads.fasta.gz",
    output:
        paf_blend="data/blend/{sample}/{region}/{platform}/alignment.paf.gz"
    conda:
        "envs/blend.yaml"
    threads: 8
    shell:
        """
        blend -x ava-ont {input.fasta} {input.fasta} | gzip -c > {output.paf_blend}
        """

rule run_daligner:
    input:
        fasta="data/regional_reads/{sample}/{region}/{platform}/reads.fasta.gz",
    output:
        maf="data/daligner/{sample}/{region}/{platform}/alignment.maf.gz",
    
    params:
        k = 16,
        average_correlation=".9",
        min_alignment_length=1000,
        min_alignment_score=50,
    threads: 8
    conda:
        "envs/daligner.yaml"
    shell:
        """
        daligner \
          -k{params.k} \
          -e{params.average_correlation} \
          -l{params.min_alignment_length} \
          -s{params.min_alignment_score} \
          -T{threads} \
          -v \
          {input.fasta} {input.fasta} \
        | gzip -c > {output.maf}
        """
    

rule get_neighbors:
    input:
        feature_matrix="data/feature_matrix/{sample}/{region}/{platform}/{encoding}/feature_matrix.npz",
        read_features="data/feature_matrix/{sample}/{region}/{platform}/{encoding}/read_features.json.gz",
        metadata="data/feature_matrix/{sample}/{region}/{platform}/{encoding}/metadata.tsv.gz",
        paf_minimap2="data/minimap2/{sample}/{region}/{platform}/alignment.paf.gz",
        paf_blend="data/blend/{sample}/{region}/{platform}/alignment.paf.gz"
    output:
        nbr_indice="data/evaluation/{sample}/{region}/{platform}/{encoding}/{method}_nbr_matrix.npz",
        time_use="data/evaluation/{sample}/{region}/{platform}/{encoding}/{method}_time_usage.json",
    benchmark:
        "data/evaluation/{sample}/{region}/{platform}/{encoding}/{method}_benchmark.csv"
    log:
        notebook="data/evaluation/{sample}/{region}/{platform}/{encoding}/{method}_get_neighbors.py.ipynb",
    threads: 1
    conda:
        "envs/evaluate.yaml"
    notebook:
        "notebooks/get_neighbors_indice.smk.py.ipynb"



rule evaluate_configs:
    input:
        feature_matrix="data/feature_matrix/{sample}/{region}/{platform}/{encoding}/feature_matrix.npz",
        read_features="data/feature_matrix/{sample}/{region}/{platform}/{encoding}/read_features.json.gz",
        metadata="data/feature_matrix/{sample}/{region}/{platform}/{encoding}/metadata.tsv.gz",
        nbr_indice="data/evaluation/{sample}/{region}/{platform}/{encoding}/{method}_nbr_matrix.npz",
        time_use="data/evaluation/{sample}/{region}/{platform}/{encoding}/{method}_time_usage.json",
    output:
        overlap="data/evaluation/{sample}/{region}/{platform}/{encoding}/{method}_overlap_stat.tsv",
    log:
        notebook="data/evaluation/{sample}/{region}/{platform}/{encoding}/{method}_evaluate_configs.py.ipynb",
    threads: 8
    conda:
        "envs/evaluate.yaml"
    notebook:
        "notebooks/evaluate_configs.smk.py.ipynb"


rule evaluate_and_visualize_configs:
    input:
        feature_matrix="data/feature_matrix/{sample}/{region}/{platform}/{encoding}/feature_matrix.npz",
        read_features="data/feature_matrix/{sample}/{region}/{platform}/{encoding}/read_features.json.gz",
        metadata="data/feature_matrix/{sample}/{region}/{platform}/{encoding}/metadata.tsv.gz",
        nbr_indice="data/evaluation/{sample}/{region}/{platform}/{encoding}/{method}_nbr_matrix.npz",
        time_use="data/evaluation/{sample}/{region}/{platform}/{encoding}/{method}_time_usage.json",

    output:
        overlap="data/evaluation/{sample}/{region}/{platform}/{encoding}/{method}_withfig_overlap_stat.tsv",
        fig="data/evaluation/{sample}/{region}/{platform}/{encoding}/{method}_fig.png",
    log:
        notebook="data/evaluation/{sample}/{region}/{platform}/{encoding}/{method}_evaluate_configs.py.ipynb",
    threads: 8
    conda:
        "envs/evaluate.yaml"
    notebook:
        "notebooks/evaluate_configs_visulization.smk.ipynb"


# ----------------------------------------------------------------------------------------------------

rule only_statistic:
    input:
        expand("data/evaluation/human/chr22/ONT_R9/kmer_k16/{method}_overlap_stat.tsv",
        method=[
        'Exact_Euclidean_None_TF-IDF',
        'Exact_Euclidean_scBiMap_500d_TF-IDF',
        'Exact_Cosine_scBiMap_500d_TF-IDF',
        'PQ_Euclidean_scBiMap_500d_TF-IDF',
        'PQ_Cosine_scBiMap_500d_TF-IDF',
        'HNSW_Euclidean_scBiMap_500d_TF-IDF',
        'HNSW_Cosine_scBiMap_500d_TF-IDF',
        'LowHash_Jaccard_None_TF-IDF',
        'WeightedLowHash_Jaccard_None_TF-IDF',
        'MinHash_Jaccard_None_TF-IDF',
        'WeightedMinHash_Jaccard_None_TF-IDF',
        'NNDescent_Euclidean_scBiMap_500d_TF-IDF',
        'Minimap2',
        'Blend'
        ])

rule statistic_and_visulization:
    input:
        expand("data/evaluation/Ecoli/all/ONT/kmer_k16/{method}_fig.png",
        method=[
        'HNSW_Euclidean_scBiMap_500d_TF-IDF',
        'WeightedLowHash_Jaccard_None_TF-IDF',
        'WeightedMinHash_Jaccard_None_TF-IDF',
        'NNDescent_Euclidean_scBiMap_500d_TF-IDF',
        ])

rule mytest:
    input:
        "data/feature_matrix/Ecoli/all/tmp/kmermetadata1.tsv.gz"